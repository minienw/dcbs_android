plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id 'kotlin-parcelize'
    id "androidx.navigation.safeargs.kotlin"
}

def appVersionCode = System.getenv("GITHUB_RUN_NUMBER") != null ? System.getenv("GITHUB_RUN_NUMBER").toInteger() : 1000000
version = "2.0"
archivesBaseName = "holder-v${version}-${appVersionCode}"

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "nl.rijksoverheid.ctr.holder"
        versionCode appVersionCode
        versionName version
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "SIGNATURE_CERTIFICATE_CN_MATCH", '".coronatester.nl"'
        buildConfigField "boolean", "FEATURE_CORONA_CHECK_API_CHECKS", "true"
        buildConfigField "boolean", "FEATURE_TEST_PROVIDER_API_CHECKS", "true"
        buildConfigField "String", "BASE_API_URL", "\"https://holder-api.coronacheck.nl/v4/\""
        buildConfigField "String", "DIGI_D_BASE_URL", "\"https://tvs.coronacheck.nl\""
        buildConfigField "String", "DIGI_D_CLIENT_ID", "\"cc_app\""
        manifestPlaceholders = [appLabel: "@string/app_name", deepLinkHost: "coronacheck.nl"]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            storeFile file('../keystore.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_KEY_ALIAS")
            keyPassword System.getenv("KEYSTORE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "environment"
    productFlavors {
        tst {
            buildConfigField "String", "BASE_API_URL", "\"https://api-ct.bananenhalen.nl/v4/\""
            buildConfigField "String", "DIGI_D_BASE_URL", "\"https://tvs.acc.coronacheck.nl\""
            buildConfigField "String", "DIGI_D_CLIENT_ID", "\"test_client\""
            buildConfigField "boolean", "FEATURE_CORONA_CHECK_API_CHECKS", "false"
            buildConfigField "boolean", "FEATURE_TEST_PROVIDER_API_CHECKS", "false"
            dimension "environment"
            versionNameSuffix "-test"
            applicationIdSuffix ".test"
            manifestPlaceholders = [appLabel: "@string/app_name_test", deepLinkHost: "web.acc.coronacheck.nl"]
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
        }
        acc {
            buildConfigField "String", "BASE_API_URL", "\"https://holder-api.acc.coronacheck.nl/v4/\""
            buildConfigField "String", "DIGI_D_BASE_URL", "\"https://tvs.acc.coronacheck.nl\""
            buildConfigField "String", "DIGI_D_CLIENT_ID", "\"test_client\""
            dimension "environment"
            versionNameSuffix "-acc"
            applicationIdSuffix ".acc"
            manifestPlaceholders = [appLabel: "@string/app_name_acc", deepLinkHost: "web.acc.coronacheck.nl"]
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
        }
        prod {
            dimension "environment"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }
}

dependencies {
    implementation project(":shared")
    implementation project(":appconfig")
    implementation project(":design")
    implementation project(":introduction")
    implementation project(":api")
    implementation project(':mobilecore')
    implementation project(":qrscanner")

    implementation "net.openid:appauth:${app_auth_version}"
    implementation "com.github.lisawray.groupie:groupie:${groupie_version}"
    implementation "com.github.lisawray.groupie:groupie-viewbinding:${groupie_version}"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "com.jakewharton.timber:timber:${timber_version}"
    implementation "com.airbnb.android:lottie:$lottie_version"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
    implementation "com.google.zxing:core:$zxing_core_version"
    implementation("com.squareup.okhttp3:logging-interceptor")
    implementation(platform("com.squareup.okhttp3:okhttp-bom:$okhttp_version"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:okhttp-tls")
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "androidx.security:security-crypto:$security_crypto_version"
    implementation "com.google.zxing:core:$zxing_core_version"
    implementation "com.scottyab:rootbeer-lib:${rootbeer_version}"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "net.zetetic:android-database-sqlcipher:$sqlcypher_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mock_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
    testImplementation("org.robolectric:robolectric:$robolectric_version") {
        exclude group: 'org.bouncycastle'
    }
    testImplementation "androidx.test:runner:$androix_test_version"
    testImplementation "com.squareup.okhttp3:mockwebserver"
    testImplementation("com.schibsted.spain:barista:$barista_version") {
        exclude group: "org.jetbrains.kotlin"
    }
    testImplementation "androidx.navigation:navigation-testing:$navigation_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "androidx.room:room-testing:$room_version"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_jdk_version"

    tstImplementation "com.google.firebase:firebase-crashlytics-ktx:$crashlytics_version"
    accImplementation "com.google.firebase:firebase-crashlytics-ktx:$crashlytics_version"

    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // Needed in order for room to work with Apple Sillicon M1
    kapt "org.xerial:sqlite-jdbc:$sqlite_jdbc"
}
